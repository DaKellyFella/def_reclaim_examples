import "forkscan.defi";
import "malloc.h";
import "pthread.h";
import "stdio.h";
import "time.h";
import "stdlib.h";

// Set data structures:
import "fhsl_lf.defi";
import "c_fhsl_lf.h";
import "bt_lf.defi";
import "c_bt_lf.h";
import "mm_ht.defi";
import "c_mm_ht.h";
import "so_ht.defi";
import "c_so_ht.h";

typedef benchmark_t = enum
    | FHSL_LF
    | C_FHSL_LF
    | BT_LF
    | C_BT_LF
    | MM_HT
    | C_MM_HT
    | SO_HT
    | C_SO_HT
    ;

typedef memory_policy_t = enum
    | POLICY_LEAKY
    | POLICY_RETIRE
    ;

typedef state_t = enum
    | STATE_WAIT
    | STATE_RUN
    | STATE_END
    ;

typedef config_t =
    {
        benchmark      benchmark_t,
        policy         memory_policy_t,
        csv            bool,
        duration_s     i32,
        thread_count   i32,
        init_size      i64,
        upper_bound    i64,
        update_rate    i32,
        structure      *void
    };

typedef stats_t =
    {
        read_attempts     i64,
        read_successes    i64,
        insert_attempts   i64,
        insert_successes  i64,
        remove_attempts   i64,
        remove_successes  i64
    };

typedef per_thread_data_t =
    {
        config         *config_t,
        id             i32,
        state          volatile *state_t,
        stats          stats_t
    };

/** Return the time in seconds.
 */
def hires_timer () -> f64
begin
    var ts timespec;
    // FIXME: Need a convenient way to access C MACROs.
    if 0 != clock_gettime(/*CLOCK_MONOTONIC=*/1, &ts) then
        fprintf(stderr, "fatal: clock failed.\n");
        exit(1);
    fi
    var sec = cast(type f64, ts.tv_sec);
    var nsec = cast(type f64, ts.tv_nsec);
    return sec + nsec / (1000.0 * 1000.0 * 1000.0);
end

def fast_rand (seed *u64) -> u64
begin
    var val = seed[0];
    if val == 0 then val = 1; fi

    val ^= val << 6;
    val ^= val >> 21;
    val ^= val << 7;

    seed[0] = val;
    return val;
end

def string_of_benchmark (b benchmark_t) -> *char
begin
    switch b with
    xcase FHSL_LF: return "fhsl_lf";
    xcase C_FHSL_LF: return "c_fhsl_lf";
    xcase BT_LF: return "bt_lf";
    xcase C_BT_LF: return "c_bt_lf";
    xcase MM_HT: return "mm_ht";
    xcase C_MM_HT: return "c_mm_ht";
    xcase SO_HT: return "so_ht";
    xcase C_SO_HT: return "c_so_ht";
    xcase _: return "unknown benchmark";
    esac
    return "FIXME: need reachability detection after a switch.";
end

def string_of_policy (p memory_policy_t) -> *char
begin
    switch p with
    xcase POLICY_LEAKY: return "leaky";
    xcase POLICY_RETIRE: return "retire";
    xcase _: return "unknown policy";
    esac
    return "FIXME: need reachability detection after a switch.";
end

def help (bench *char) -> void
begin
    printf("Usage: %s [OPTIONS]\n", bench);
    printf("  -h, --help: This help message.\n");
    printf("  -t <n>: Set the number of threads. (default = 1)\n");
    printf("  -d <n>: Benchmark duration in seconds. (default = 1)\n");
    printf("  -b <benchmark>: Set the benchmark. (default = fhsl_lf)\n");
    printf("     * fhsl_lf: Fixed-height skip list; lock-free. Written in DEF.\n");
    printf("     * c_fhsl_b: Fixed-height skip list; lock-free. Written in C.\n");
    printf("     * bt_lf: Use the lock-free binary tree written in DEF.\n");
    printf("     * c_bt_lf: Use the lock-free binary tree written in C.\n");
    printf("     * mm_ht: Use the Maged Michael lock-free hash table in DEF.\n");
    printf("     * c_mm_ht: Use the Maged Michael lock-free hash table in C.\n");
    printf("     * so_ht: Use the Split-Order lock-free hash table in DEF.\n");
    printf("     * c_so_ht: Use the Split-Order lock-free hash table in C.\n");
    printf("  -p <mem_policy>: Set the memory policy. (default = leaky)\n");
    printf("     * leaky: Leak removed nodes.\n");
    printf("     * retire: Use Forkscan to reclaim removed nodes.\n");
    printf("  -i <n>: Initial set size. (default = 256)\n");
    printf("  -r <n>: Range upper bound [0-n). (default = 512)\n");
    printf("  -u <n>: Percent of ops that are updates. (default = 10)\n");
    printf("  --csv: Generate a comma-separated value summary.\n");
    exit(127);
end

/** Parse an i32 from txt in the range [low, high].  The err text is the
 *  command line option and is used in case of failure.
 */
def read_i32 (low i32, high i32, txt *char, err *char) -> i32
begin
    var n = atoi(txt);
    if n < low || n > high then
        fprintf(stderr, "error: %s requires an argument between %d and %d\n",
                err, low, high);
        exit(1);
    fi
    return n;
end

/** Parse an i64 from txt in the range [low, high].  The err text is the
 *  command line option and is used in case of failure.
 */
def read_i64 (low i64, high i64, txt *char, err *char) -> i64
begin
    var n = atoll(txt);
    if n < low || n > high then
        fprintf(stderr, "error: %s requires an argument between %lld and %lld\n",
                err, low, high);
        exit(1);
    fi
    return n;
end

def read_args (argc i32, argv **char) -> config_t
begin
    var config config_t =
        { FHSL_LF, POLICY_RETIRE, false, 1, 1, 256, 512, 10, nil };

    for var i = 1; i < argc; ++i do
        switch argv[i] with
        xcase "-h":
        ocase "--help":
            help(argv[0]); // no return.
        xcase "-t":
            ++i;
            if i >= argc then
                fprintf(stderr, "error: -t requires an argument.\n");
                exit(1);
            fi
            config.thread_count = read_i32(1, 128, argv[i], "-t");
        xcase "-d":
            ++i;
            if i >= argc then
                fprintf(stderr, "error: -d requires an argument.\n");
                exit(1);
            fi
            config.duration_s = read_i32(1, 999, argv[i], "-d");
        xcase "-b":
            ++i;
            if i >= argc then
                fprintf(stderr, "error: -b requires an argument.\n");
                exit(1);
            fi
            switch argv[i] with
            xcase "fhsl_lf": config.benchmark = FHSL_LF;
            xcase "c_fhsl_lf": config.benchmark = C_FHSL_LF;
            xcase "bt_lf": config.benchmark = BT_LF;
            xcase "c_bt_lf": config.benchmark = C_BT_LF;
            xcase "mm_ht": config.benchmark = MM_HT;
            xcase "c_mm_ht": config.benchmark = C_MM_HT;
            xcase "so_ht": config.benchmark = SO_HT;
            xcase "c_so_ht": config.benchmark = C_SO_HT;
            xcase _:
                printf("unknown benchmark: %s\n", argv[i]);
                exit(1);
            esac
        xcase "-p":
            ++i;
            if i >= argc then
                fprintf(stderr, "error: -b requires an argument.\n");
                exit(1);
            fi
            switch argv[i] with
            xcase "leaky": config.policy = POLICY_LEAKY;
            xcase "retire":
            ocase "forkscan":
                config.policy = POLICY_RETIRE;
            xcase _:
                printf("unknown memory policy: %s\n", argv[i]);
                exit(1);
            esac
        xcase "-i":
            ++i;
            if i >= argc then
                fprintf(stderr, "error: -i requires an argument.\n");
                exit(1);
            fi
            config.init_size =
                read_i64(1, 0x7FFFFFFFFFFFFFFFI64, argv[i], "-i");
        xcase "-r":
            ++i;
            if i >= argc then
                fprintf(stderr, "error: -r requires an argument.\n");
                exit(1);
            fi
            config.upper_bound =
                read_i64(1, 0x7FFFFFFFFFFFFFFFI64, argv[i], "-r");
        xcase "-u":
            ++i;
            if i >= argc then
                fprintf(stderr, "error: -u requires an argument.\n");
                exit(1);
            fi
            config.update_rate = read_i32(0, 100, argv[i], "-u");
        xcase "--csv":
            config.csv = true;
        xcase _:
            printf("unknown option: %s\n", argv[i]);
            exit(1);
        esac
    od
    return config;
end

def verify_config (config *config_t) -> void
begin
    // Be sure the configuration is valid -- i.e., does the given data
    // structure support the requested memory policy, etc.?

    switch { config.benchmark, config.policy } with
    xcase { FHSL_LF, POLICY_RETIRE }:
    ocase { FHSL_LF, POLICY_LEAKY }:
    ocase { C_FHSL_LF, POLICY_LEAKY }:
    ocase { BT_LF, POLICY_RETIRE }:
    ocase { BT_LF, POLICY_LEAKY }:
    ocase { C_BT_LF, POLICY_LEAKY }:
    ocase { MM_HT, POLICY_RETIRE }:
    ocase { MM_HT, POLICY_LEAKY }:
    ocase { C_MM_HT, POLICY_LEAKY }:
    ocase { SO_HT, POLICY_RETIRE }:
    ocase { SO_HT, POLICY_LEAKY }:
    ocase { C_SO_HT, POLICY_LEAKY }:
    xcase _:
        printf("Unsupported configuration:\n");
        printf("  benchmark: %s\n  policy: %s\n",
               string_of_benchmark(config.benchmark),
               string_of_policy(config.policy));
        printf("No implementation for this combination.\n");
        exit(1);
    esac
end

def print_config (config *config_t) -> void
begin
    printf("Benchmark configuration\n");
    printf("--------- -------------\n");
    printf("  benchmark    : %s\n", string_of_benchmark(config.benchmark));
    printf("  mem_policy   : %s\n", string_of_policy(config.policy));
    printf("  duration (s) : %d\n", config.duration_s);
    printf("  thread count : %d\n", config.thread_count);
    printf("  initial size : %lld\n", config.init_size);
    printf("  range        : [0-%lld)\n", config.upper_bound);
    printf("  updates      : %d%%\n", config.update_rate);

    puts(""); // blank line.
end

def success_rate (attempts i64, successes i64) -> f64
begin
    if attempts == 0 then return 0.0F64; fi
    return cast(type f64, successes) * 100.0F64 / cast(type f64, attempts);
end

def print_stats (stats *stats_t, runtime f64) -> void
begin
    var total_ops i64 = 0;
    printf("  read-attempts      : %lld\n", stats.read_attempts);
    printf("  read-successes     : %lld (%.1f%%)\n", stats.read_successes,
           success_rate(stats.read_attempts, stats.read_successes));
    printf("  reads-per-second   : %lld\n",
           cast(type i64, stats.read_successes / runtime));
    printf("  insert-attempts    : %lld\n", stats.insert_attempts);
    printf("  insert-successes   : %lld (%.1f%%)\n", stats.insert_successes,
           success_rate(stats.insert_attempts, stats.insert_successes));
    printf("  inserts-per-second : %lld\n",
           cast(type i64, stats.insert_successes / runtime));
    printf("  remove-attempts    : %lld\n", stats.remove_attempts);
    printf("  remove-successes   : %lld (%.1f%%)\n", stats.remove_successes,
           success_rate(stats.remove_attempts, stats.remove_successes));
    printf("  removes-per-second : %lld\n",
           cast(type i64, stats.remove_successes / runtime));

    total_ops = stats.read_attempts
        + stats.insert_attempts
        + stats.remove_attempts;

    printf("  total-operations   : %lld\n", total_ops);
    printf("  ops-per-second     : %lld\n",
           cast(type i64, total_ops / runtime));
end

def print_csv (config *config_t, stats *stats_t, runtime f64) -> void
begin
    var keys *FILE = fopen("set_keys.csv", "w");
    fputs("name, benchmark, policy, threads, init_size, upper_bound, update_rate, ops/sec\n", keys);

    var total_ops = stats.read_attempts
        + stats.insert_attempts
        + stats.remove_attempts;
    var data *FILE = fopen("set_data.csv", "a");
    fprintf(data, "set_bench, %s, %s, %d, %lld, %lld, %d, %lld\n",
            string_of_benchmark(config.benchmark),
            string_of_policy(config.policy),
            config.thread_count,
            config.init_size,
            config.upper_bound,
            config.update_rate,
            cast(type i64, total_ops / runtime));
end

def thread (arg *void) -> *void
begin
    var seed = cast(type u64, time(nil));
    var ptd = cast(type volatile *per_thread_data_t, arg);
    var stats stats_t = { 0, 0, 0, 0, 0, 0 };
    var config *config_t = ptd.config;
    var bench = config.benchmark;
    var policy = config.policy;
    var set = config.structure;

    printf("[started thread %d]\n", ptd.id);
    while ptd.state[0] == STATE_WAIT do
        // busy-wait.
    od
    var read_action = 100 - config.update_rate;
    var add_action = read_action + (config.update_rate / 2);
    var pqueue_action bool = (fast_rand(&seed) % 100) < 50;
    while ptd.state[0] == STATE_RUN do
        var action = fast_rand(&seed) % 100;
        var val i64 = fast_rand(&seed) % config.upper_bound;
        switch bench with
/***************************************************************************/
/*            fixed-height skip list, lock free written in DEF             */
/***************************************************************************/
        xcase FHSL_LF:
            if action < read_action then
                stats.read_attempts++;
                if fhsl_lf_contains(set, val) then
                    stats.read_successes++;
                fi
            elif action < add_action then
                stats.insert_attempts++;
                if fhsl_lf_add(&seed, set, val) then
                    stats.insert_successes++;
                fi
            else
                stats.remove_attempts++;
                switch policy with
                xcase POLICY_RETIRE:
                    if fhsl_lf_remove(set, val) then
                        stats.remove_successes++;
                    fi
                xcase POLICY_LEAKY:
                    if fhsl_lf_leaky_remove(set, val) then
                        stats.remove_successes++;
                    fi
                xcase _:
                    printf("error: unsupported mem policy for benchmark.\n");
                    exit(1);
                esac
            fi
/***************************************************************************/
/*            fixed-height skip list, lock free written in C               */
/***************************************************************************/
        xcase C_FHSL_LF:
            if action < read_action then
                stats.read_attempts++;
                if c_fhsl_lf_contains(set, val) == 1 then
                    stats.read_successes++;
                fi
            elif action < add_action then
                stats.insert_attempts++;
                if c_fhsl_lf_add(&seed, set, val) == 1 then
                    stats.insert_successes++;
                fi
            else
                stats.remove_attempts++;
                switch policy with
                xcase POLICY_LEAKY:
                    if c_fhsl_lf_remove_leaky(set, val) == 1 then
                        stats.remove_successes++;
                    fi
                xcase _:
                    printf("error: unsupported mem policy for benchmark.\n");
                    exit(1);
                esac
            fi
/***************************************************************************/
/*                   lock-free binary tree written in DEF                  */
/***************************************************************************/
        xcase BT_LF:
            if action < read_action then
                stats.read_attempts++;
                if bt_lf_contains(set, val) then
                    stats.read_successes++;
                fi
            elif action < add_action then
                stats.insert_attempts++;
                if bt_lf_add(set, val) then
                    stats.insert_successes++;
                fi
            else
                stats.remove_attempts++;
                switch policy with
                xcase POLICY_RETIRE:
                    if bt_lf_remove_retire(set, val) then
                        stats.remove_successes++;
                    fi
                xcase POLICY_LEAKY:
                    if bt_lf_remove_leaky(set, val) then
                        stats.remove_successes++;
                    fi
                xcase _:
                    printf("error: unsupported mem policy for benchmark.\n");
                    exit(1);
                esac
            fi
/***************************************************************************/
/*                      lock-free binary tree written in C                 */
/***************************************************************************/
        xcase C_BT_LF:
            if action < read_action then
                stats.read_attempts++;
                if 0 != c_bt_lf_contains(set, val) then
                    stats.read_successes++;
                fi
            elif action < add_action then
                stats.insert_attempts++;
                if 0 != c_bt_lf_add(set, val) then
                    stats.insert_successes++;
                fi
            else
                stats.remove_attempts++;
                switch policy with
                xcase POLICY_LEAKY:
                    if 0 != c_bt_lf_remove_leaky(set, val) then
                        stats.remove_successes++;
                    fi
                xcase _:
                    printf("error: unsupported mem policy for benchmark.\n");
                    exit(1);
                esac
            fi
/***************************************************************************/
/*           Maged Michael lock-free hash table written in DEF             */
/***************************************************************************/
        xcase MM_HT:
            if action < read_action then
                stats.read_attempts++;
                if mm_ht_contains(set, val) then
                    stats.read_successes++;
                fi
            elif action < add_action then
                stats.insert_attempts++;
                if mm_ht_add(set, val) then
                    stats.insert_successes++;
                fi
            else
                stats.remove_attempts++;
                switch policy with
                xcase POLICY_RETIRE:
                    if mm_ht_remove_retire(set, val) then
                        stats.remove_successes++;
                    fi
                xcase POLICY_LEAKY:
                    if mm_ht_remove_leaky(set, val) then
                        stats.remove_successes++;
                    fi 
                xcase _:
                    printf("error: unsupported mem policy for benchmark.\n");
                    exit(1);
                esac
            fi
/***************************************************************************/
/*             Maged Michael lock-free hash table written in C             */
/***************************************************************************/
        xcase C_MM_HT:
            if action < read_action then
                stats.read_attempts++;
                if 0 != c_mm_ht_contains(set, val) then
                    stats.read_successes++;
                fi
            elif action < add_action then
                stats.insert_attempts++;
                if 0 != c_mm_ht_add(set, val) then
                    stats.insert_successes++;
                fi
            else
                stats.remove_attempts++;
                switch policy with
                xcase POLICY_LEAKY:
                    if 0 != c_mm_ht_remove_leaky(set, val) then
                        stats.remove_successes++;
                    fi
                xcase _:
                    printf("error: unsupported mem policy for benchmark.\n");
                    exit(1);
                esac
            fi
/***************************************************************************/
/*             Split-Order lock-free hash table written in DEF             */
/***************************************************************************/
        xcase SO_HT:
            if action < read_action then
                stats.read_attempts++;
                if so_ht_contains(set, val) then
                    stats.read_successes++;
                fi
            elif action < add_action then
                stats.insert_attempts++;
                if so_ht_add(set, val) then
                    stats.insert_successes++;
                fi
            else
                stats.remove_attempts++;
                switch policy with
                xcase POLICY_RETIRE:
                    if so_ht_remove_retire(set, val) then
                        stats.remove_successes++;
                    fi
                xcase POLICY_LEAKY:
                    if so_ht_remove_leaky(set, val) then
                        stats.remove_successes++;
                    fi
                xcase _:
                    printf("error: unsupported mem policy for benchmark.\n");
                    exit(1);
                esac
            fi
/***************************************************************************/
/*              Split-Order lock-free hash table written in C              */
/***************************************************************************/
        xcase C_SO_HT:
            if action < read_action then
                stats.read_attempts++;
                if 0 != c_so_ht_contains(set, val) then
                    stats.read_successes++;
                fi
            elif action < add_action then
                stats.insert_attempts++;
                if 0 != c_so_ht_add(set, val) then
                    stats.insert_successes++;
                fi
            else
                stats.remove_attempts++;
                switch policy with
                xcase POLICY_LEAKY:
                    if 0 != c_so_ht_remove_leaky(set, val) then
                        stats.remove_successes++;
                    fi
                xcase _:
                    printf("error: unsupported mem policy for benchmark.\n");
                    exit(1);
                esac
            fi
        xcase _:
            printf("error: unknown benchmark configuration.\n");
            exit(1);
        esac
    od
    printf("FINISHED\n");

    // Store this thread's statistics in the per-thread-data.
    ptd.stats = stats;
    return nil;
end

def initialize_sructure (config *config_t, seed *u64) -> void
begin
    switch config.benchmark with
    xcase FHSL_LF:
        var set = fhsl_lf_create();
        for var i = 0; i < config.init_size; ++i do
            var val = fast_rand(seed) % config.upper_bound;
            if false == fhsl_lf_add(seed, set, val) then
                --i;
            fi
        od
        config.structure = set;
    xcase C_FHSL_LF:
        var set = c_fhsl_lf_create();
        for var i = 0; i < config.init_size; ++i do
            var val = fast_rand(seed) % config.upper_bound;
            if 0 == c_fhsl_lf_add(seed, set, val) then
                --i;
            fi
        od
        config.structure = cast(type *void, set);
    xcase BT_LF:
        var set = bt_lf_create();
        for var i = 0; i < config.init_size; ++i do
            var val = fast_rand(seed) % config.upper_bound;
            if false == bt_lf_add(set, val) then
                --i;
            fi 
        od
        config.structure = set;
    xcase C_BT_LF:
        var set = c_bt_lf_create();
        for var i = 0; i < config.init_size; ++i do
            var val = fast_rand(seed) % config.upper_bound;
            if 0 == c_bt_lf_add(set, val) then
                --i;
            fi
        od
        config.structure = set;
    xcase MM_HT:
        var set = mm_ht_create(config.upper_bound);
        for var i = 0; i < config.init_size; ++i do
            var val = fast_rand(seed) % config.upper_bound;
            if !mm_ht_add(set, val) then
                --i;
            fi
        od
        config.structure = set;
    xcase C_MM_HT:
        var set = c_mm_ht_create(config.upper_bound);
        for var i = 0; i < config.init_size; ++i do
            var val = fast_rand(seed) % config.upper_bound;
            if 0 == c_mm_ht_add(set, val) then
                --i;
            fi
        od
        config.structure = set;
    xcase SO_HT:
        var set = so_ht_create(config.upper_bound, 5);
        for var i = 0; i < config.init_size; ++i do
            var val = fast_rand(seed) % config.upper_bound;
            if !so_ht_add(set, val) then
                --i;
            fi
        od
        config.structure = set;
    xcase C_SO_HT:
        var set = c_so_ht_create(config.upper_bound, 5);
        for var i = 0; i < config.init_size; ++i do
            var val = fast_rand(seed) % config.upper_bound;
            if 0 == c_so_ht_add(set, val) then
                --i;
            fi
        od
        config.structure = set;
    xcase _:
        printf("error: unable to initialize unknown set.\n");
        exit(1);
    esac
end

export
def main (argc i32, argv **char) -> i32
begin
    var config = read_args(argc, argv);
    var seed = cast(type u64, time(nil));
    var state = STATE_WAIT;

    forkscan_set_allocator(malloc, free, malloc_usable_size);

    verify_config(&config);
    print_config(&config);

    printf("Initializing set.\n");
    initialize_sructure(&config, &seed);

    printf("Starting threads.\n");
    var tids *pthread_t = new [config.thread_count]pthread_t;
    var ptds *per_thread_data_t = new [config.thread_count]per_thread_data_t;
    for var i = 0; i < config.thread_count; ++i do
        ptds[i] =
            { &config,
              i,
              &state,
              { 0, 0, 0, 0, 0, 0 }
            };

        var ret = pthread_create(&tids[i], nil, thread, &ptds[i]);
        if ret != 0 then
            printf("error: failed to create thread id: %d\n", i);
            exit(1);
        fi
    od

    puts("beginning");

    var start_time = hires_timer();
    state = STATE_RUN;
    // Robust sleep against Forkscan signals.
    forkscan_sleep(config.duration_s);
    state = STATE_END;

    puts("ending");
    printf("Joining threads.\n");
    for var i = 0; i < config.thread_count; ++i do
        var ret = pthread_join(tids[i], nil);
        if ret != 0 then
            printf("error: failed to join thread id: %d\n", i);
            exit(1);
        fi
        printf("[joined thread %d]\n", i);
    od
    var runtime = hires_timer() - start_time;

    // Print out the statistics.
    puts("Summary:");
    printf("  runtime (s) : %.9f\n", runtime);

    var totals stats_t = { 0, 0, 0, 0, 0, 0 };
    for var i = 0; i < config.thread_count; ++i do
        printf("statistics for thread %d\n", i);
        print_stats(&ptds[i].stats, runtime);
        totals.read_attempts += ptds[i].stats.read_attempts;
        totals.read_successes += ptds[i].stats.read_successes;
        totals.insert_attempts += ptds[i].stats.insert_attempts;
        totals.insert_successes += ptds[i].stats.insert_successes;
        totals.remove_attempts += ptds[i].stats.remove_attempts;
        totals.remove_successes += ptds[i].stats.remove_successes;
    od

    printf("total statistics:\n");
    print_stats(&totals, runtime);
    if config.csv then print_csv(&config, &totals, runtime); fi

    delete tids;
    delete ptds;
    return 0;
end
